# Maintainer: Ryan Hughes <ryan@omarchy.org>

pkgname=omarchy-zsh
pkgver=1.3.0
pkgrel=1
pkgdesc="Omarchy shell configuration for Zsh"
arch=('any')
url="https://github.com/omacom-io/omarchy-zsh"
license=('MIT')
depends=('zsh' 'eza' 'mise' 'zoxide' 'starship' 'fzf' 'fd' 'bat')
options=('!debug')
install=omarchy-zsh.install
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/heads/master.tar.gz"
        "omadots.tar.gz::https://github.com/omacom-io/omadots/archive/refs/heads/master.tar.gz")
sha256sums=('SKIP'
            'SKIP')

package() {
  cd "${srcdir}/${pkgname}-master"

  # Install shared shell config from omadots (aliases, functions, envs, inits, inputrc)
  install -dm755 "${pkgdir}/usr/share/${pkgname}/shell"
  cp "${srcdir}/omadots-master/config/shell/"* "${pkgdir}/usr/share/${pkgname}/shell/"

  # Rewrite paths to source from installed package location instead of user config
  sed -i 's|\$HOME/\.config/shell|/usr/share/omarchy-zsh/shell|g' "${pkgdir}/usr/share/${pkgname}/shell/all"

  # Install zsh-specific zoptions from omarchy-zsh
  cp shell/zoptions "${pkgdir}/usr/share/${pkgname}/shell/"

  # Install templates and documentation
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -r templates LICENSE README.md "${pkgdir}/usr/share/${pkgname}/"

  # Install setup script
  install -Dm755 bin/omarchy-setup-zsh "${pkgdir}/usr/bin/omarchy-setup-zsh"
}
