# Maintainer: Ryan Hughes <ryan@omarchy.org>

pkgname=omarchy-fish
pkgver=1.1.0
pkgrel=1
pkgdesc="Omarchy shell configuration for Fish"
arch=('any')
url="https://github.com/omacom-io/omarchy-fish"
license=('MIT')
depends=('fish' 'eza' 'mise' 'zoxide' 'starship' 'fzf' 'fd' 'bat')
options=('!debug')
install=omarchy-fish.install
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "fzf.fish-v10.3.tar.gz::https://github.com/PatrickF1/fzf.fish/archive/refs/tags/v10.3.tar.gz")
sha256sums=('8ca2d3b63e774a43eb96c99d6153ac7495bd8b8ee93a2e977d7767e1d9ac45a5'
            'a0d56e974b8d5144c3262dcf87630c8886407d28a2f9e626e46bc18199fe6f97')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install fzf.fish to vendor directories
  install -dm755 "${pkgdir}/usr/share/fish/vendor_conf.d"
  install -dm755 "${pkgdir}/usr/share/fish/vendor_functions.d"
  install -dm755 "${pkgdir}/usr/share/fish/vendor_completions.d"
  cp "${srcdir}/fzf.fish-10.3/conf.d/"*.fish "${pkgdir}/usr/share/fish/vendor_conf.d/"
  cp "${srcdir}/fzf.fish-10.3/functions/"*.fish "${pkgdir}/usr/share/fish/vendor_functions.d/"
  cp "${srcdir}/fzf.fish-10.3/completions/"*.fish "${pkgdir}/usr/share/fish/vendor_completions.d/"

  # Install omarchy-fish conf.d and functions
  cp conf.d/*.fish "${pkgdir}/usr/share/fish/vendor_conf.d/"
  cp functions/*.fish "${pkgdir}/usr/share/fish/vendor_functions.d/"

  # Install templates and documentation
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -r templates LICENSE README.md "${pkgdir}/usr/share/${pkgname}/"
  cp "${srcdir}/fzf.fish-10.3/LICENSE.md" "${pkgdir}/usr/share/${pkgname}/LICENSE.fzf.fish"
  cp "${srcdir}/fzf.fish-10.3/README.md" "${pkgdir}/usr/share/${pkgname}/README.fzf.fish.md"

  # Install setup script
  install -Dm755 bin/omarchy-setup-fish "${pkgdir}/usr/bin/omarchy-setup-fish"
}
