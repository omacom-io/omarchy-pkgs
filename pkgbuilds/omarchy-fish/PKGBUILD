# Maintainer: Ryan Hughes <ryan@omarchy.org>

pkgname=omarchy-fish
pkgver=1.0.0
pkgrel=1
pkgdesc="Omarchy shell configuration for Fish"
arch=('any')
url="https://github.com/omacom-io/omarchy-fish"
license=('MIT')
depends=('fish' 'eza' 'mise' 'zoxide' 'starship' 'fzf' 'fd' 'bat')
install=omarchy-fish.install
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "fzf.fish-v10.3.tar.gz::https://github.com/PatrickF1/fzf.fish/archive/refs/tags/v10.3.tar.gz")
sha256sums=('85e417fe0035698c1e0ca472369cb68847ecdf2d19f475f75cc1d0f364607361'
            'a0d56e974b8d5144c3262dcf87630c8886407d28a2f9e626e46bc18199fe6f97')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install omarchy-fish conf.d files to vendor_conf.d
  install -dm755 "${pkgdir}/usr/share/fish/vendor_conf.d"
  install -Dm644 conf.d/envs.fish "${pkgdir}/usr/share/fish/vendor_conf.d/omarchy-fish-envs.fish"
  install -Dm644 conf.d/init.fish "${pkgdir}/usr/share/fish/vendor_conf.d/omarchy-fish-init.fish"

  # Install fzf.fish functions first
  install -dm755 "${pkgdir}/usr/share/fish/vendor_functions.d"
  install -Dm644 "${srcdir}/fzf.fish-10.3/functions/"*.fish "${pkgdir}/usr/share/fish/vendor_functions.d/"
  install -Dm644 "${srcdir}/fzf.fish-10.3/conf.d/fzf.fish" "${pkgdir}/usr/share/fish/vendor_conf.d/fzf.fish"

  # Install omarchy-fish functions (overrides fzf.fish customizations)
  # Use find to include hidden files (....fish and .....fish)
  find functions -name "*.fish" -exec install -Dm644 {} "${pkgdir}/usr/share/fish/vendor_functions.d/" \;

  install -dm755 "${pkgdir}/usr/share/fish/vendor_completions.d"
  install -Dm644 "${srcdir}/fzf.fish-10.3/completions/"*.fish "${pkgdir}/usr/share/fish/vendor_completions.d/"

  # Install templates and documentation to /usr/share/omarchy-fish
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp templates/bashrc "${pkgdir}/usr/share/${pkgname}/bashrc"
  cp LICENSE "${pkgdir}/usr/share/${pkgname}/LICENSE"
  cp README.md "${pkgdir}/usr/share/${pkgname}/README.md"
  cp "${srcdir}/fzf.fish-10.3/LICENSE.md" "${pkgdir}/usr/share/${pkgname}/LICENSE.fzf.fish"
  cp "${srcdir}/fzf.fish-10.3/README.md" "${pkgdir}/usr/share/${pkgname}/README.fzf.fish.md"

  # Install setup script
  install -Dm755 bin/omarchy-setup-fish "${pkgdir}/usr/bin/omarchy-setup-fish"
}
