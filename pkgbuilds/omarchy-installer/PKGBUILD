# Maintainer: Ryan Hughes <ryan@omarchy.org>
pkgname='omarchy-installer'
pkgver=3.2.0
pkgrel=1
pkgdesc='Installation scripts and system migrations for Omarchy'
arch=('any')
url='https://github.com/basecamp/omarchy'
license=('MIT')
provides=('omarchy-installer')

depends=(
  'gum'            # Required by installer scripts
  'jq'             # Required by installer scripts
  'git'            # Required by installer scripts
  'python-archinstall'  # Required by omarchy-install
)

optdepends=(
  'omarchy-settings: User and system configurations'
)

source=('omarchy-installer::git+https://github.com/basecamp/omarchy.git')
sha256sums=('SKIP')

package() {
  cd "omarchy-installer"

  # Main install script
  install -Dm755 install.sh "$pkgdir/usr/share/omarchy/install.sh"

  # Install scripts and helpers
  install -d "$pkgdir/usr/share/omarchy/install"
  for dir in install/*/; do
    cp -r "$dir" "$pkgdir/usr/share/omarchy/install/"
  done

  for file in install/*; do
    [[ -f "$file" ]] && cp "$file" "$pkgdir/usr/share/omarchy/install/"
  done

  # Installation wrapper scripts
  install -d "$pkgdir/usr/share/omarchy/bin"
  install -Dm755 bin/omarchy-install "$pkgdir/usr/share/omarchy/bin/omarchy-install"
  install -Dm755 bin/omarchy-disk-config "$pkgdir/usr/share/omarchy/bin/omarchy-disk-config"
  install -Dm755 bin/omarchy-install-final-configurations.sh "$pkgdir/usr/share/omarchy/bin/omarchy-install-final-configurations.sh"
}
