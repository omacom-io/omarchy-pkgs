# Maintainer: Ryan Hughes <ryan@omarchy.org>
pkgname='omarchy-settings'
pkgver=3.2.0
pkgrel=1
pkgdesc='Configuration files and user defaults for Omarchy'
arch=('any')
url='https://github.com/basecamp/omarchy'
license=('MIT')
provides=('omarchy-settings')
install='omarchy-settings.install'

backup=(
  'etc/skel/.bashrc'
  'etc/security/faillock.conf'
  'etc/nsswitch.conf'
  'etc/plymouth/plymouthd.conf'
  'etc/cups/cups-browsed.conf'
)

depends=(
  'bash'
  'curl'           # Required by omarchy-upload-log
  'gum'            # Required by omarchy-debug and install helpers
  'plymouth'       # For boot theme
)

optdepends=(
  'omarchy: Full Omarchy desktop environment'
  'inxi: System information for omarchy-debug'
  'fastfetch: System information for omarchy-upload-log'
)

source=('omarchy-installer::git+https://github.com/basecamp/omarchy.git')
sha256sums=('SKIP')

package() {
  cd "omarchy-installer"

  # User skeleton files (/etc/skel/)
  install -d "$pkgdir/etc/skel/.config"
  cp -r config/* "$pkgdir/etc/skel/.config/"
  rm -rf "$pkgdir/etc/skel/.config/.local"

  install -d "$pkgdir/etc/skel/.local/share/applications"
  cp applications/*.desktop "$pkgdir/etc/skel/.local/share/applications/"
  cp applications/hidden/*.desktop "$pkgdir/etc/skel/.local/share/applications/"

  install -d "$pkgdir/etc/skel/.local/share/applications/icons"
  cp applications/icons/*.png "$pkgdir/etc/skel/.local/share/applications/icons/"

  # System configuration files (/etc/)
  install -d "$pkgdir/etc"
  cp -r etc/* "$pkgdir/etc/"

  if [ -d "$pkgdir/etc/sudoers.d" ]; then
    chmod 440 "$pkgdir/etc/sudoers.d/"* 2>/dev/null || true
  fi

  # Move conflicting files to etc-overrides for post_install
  install -d "$pkgdir/usr/share/omarchy/etc-overrides"
  install -Dm644 default/bashrc "$pkgdir/usr/share/omarchy/etc-overrides/dot.bashrc"
  mv "$pkgdir/etc/security/faillock.conf" "$pkgdir/usr/share/omarchy/etc-overrides/security-faillock.conf"
  mv "$pkgdir/etc/nsswitch.conf" "$pkgdir/usr/share/omarchy/etc-overrides/nsswitch.conf"
  mv "$pkgdir/etc/plymouth/plymouthd.conf" "$pkgdir/usr/share/omarchy/etc-overrides/plymouth-plymouthd.conf"
  mv "$pkgdir/etc/cups/cups-browsed.conf" "$pkgdir/usr/share/omarchy/etc-overrides/cups-cups-browsed.conf"

  # Plymouth theme
  install -d "$pkgdir/usr/share/plymouth/themes/omarchy"
  cp default/plymouth/* "$pkgdir/usr/share/plymouth/themes/omarchy/"

  # Default configs and assets
  install -d "$pkgdir/usr/share/omarchy"
  cp -r default "$pkgdir/usr/share/omarchy/"

  # System fonts
  install -Dm644 config/.local/share/fonts/omarchy.ttf "$pkgdir/usr/share/fonts/omarchy/omarchy.ttf"

  # ISO/Installation helper binaries
  install -Dm755 bin/omarchy-upload-log "$pkgdir/usr/bin/omarchy-upload-log"
  install -Dm755 bin/omarchy-debug "$pkgdir/usr/bin/omarchy-debug"

  # Branding assets
  install -Dm644 logo.txt "$pkgdir/usr/share/omarchy/logo.txt"
  install -Dm644 icon.png "$pkgdir/usr/share/omarchy/icon.png"
  install -Dm644 icon.txt "$pkgdir/usr/share/omarchy/icon.txt"
  install -Dm644 logo.svg "$pkgdir/usr/share/omarchy/logo.svg"
  install -Dm644 version "$pkgdir/usr/share/omarchy/version"

}
