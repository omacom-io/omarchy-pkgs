# Maintainer: Daniel Chesters <archlinux@coin-coin.xyz>
# Contributor: RiverOnVenus <error@zhui.dev>
pkgname=python-terminaltexteffects
_pkgname=terminaltexteffects
pkgver=0.12.2
pkgrel=1
_commit=1613a8cb77abfc836e98590f3c2c5f70125a1b41
pkgdesc='Visual effects engine applied to text in the terminal. '
url="https://github.com/ChrisBuilds/terminaltexteffects"
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-hatchling')
provides=('terminaltexteffects')
conflicts=('terminaltexteffects')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ChrisBuilds/terminaltexteffects/archive/$_commit.tar.gz")

build() {
	cd "$_pkgname-$_commit" || exit
	python -m build --wheel --no-isolation
}

package() {
	cd "$_pkgname-$_commit" || exit
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: set ts=2 sw=2 et:
b2sums=('770f52cc34f8eb25c279ee2d9d45910156ebb1dab6cdffc168730d49151f9af657a198d8690eba6e4f106abfc57c4c8e012954bd85fd1652e3c8616197c3bbd5')
